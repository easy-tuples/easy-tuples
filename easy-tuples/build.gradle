plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(16)
    }

    withJavadocJar()
    withSourcesJar()
}

javadoc {
    options.addBooleanOption('html5', true)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'io.github.easytuples'
            artifactId = 'easy-tuples'
            version = '1.0.0'

            from components.java
            pom {
                name = 'Easy Tuples'
                description = 'Easy, structural tuples for Java'
                url = 'https://easytuples.github.io/easy-tuples/'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'vab2048'
                        name = 'Vikram Bakshi'
                        email = 'vab2048@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/easytuples/easy-tuples.git'
                    developerConnection = 'scm:git:ssh://github.com/easytuples/easy-tuples.git'
                    url = 'https://github.com/easytuples/easy-tuples/tree/main'
                }
            }
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            def releasesRepoUrl = "https://s01.oss.sonatype.org/content/repositories/releases/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials(PasswordCredentials)
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

dependencies {
    testImplementation(platform('org.junit:junit-bom:5.7.1'))
    testImplementation('org.junit.jupiter:junit-jupiter')
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}